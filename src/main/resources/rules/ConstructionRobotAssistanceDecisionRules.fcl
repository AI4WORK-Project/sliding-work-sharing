FUNCTION_BLOCK ConstructionRobotAssistanceDecision

VAR_INPUT
    timeRobotIsBlocked : REAL;
    robotBatteryStatus : REAL;
    waitingTimeForHuman : REAL;
END_VAR

VAR_OUTPUT
    suggestedWorkSharingApproach : REAL;
END_VAR

FUZZIFY timeRobotIsBlocked     // in minutes (0 to 15 minutes)
    TERM SHORT := (0, 1) (4, 1) (6, 0);
    TERM MODERATE := (4, 0) (6, 1) (9, 1) (11, 0);
    TERM LONG := (9, 0) (11, 1) (15, 1);
END_FUZZIFY

FUZZIFY robotBatteryStatus    // in percent (0 to 100 percent)
    TERM LOW := (0, 1) (20, 1) (30, 0);   // low battery, 0 to 30 percent
END_FUZZIFY

FUZZIFY waitingTimeForHuman   // in minutes (0 to 15 minutes)
    TERM LOW := (0, 1) (5, 1) (10, 0);
    TERM HIGH := (5, 0) (10, 1) (15, 1);
END_FUZZIFY

DEFUZZIFY suggestedWorkSharingApproach
    TERM HUMAN_MANUALLY := (0, 1) (1, 1) (1, 0);   // Ask human for help
    TERM HUMAN_IN_THE_LOOP := (1, 0) (1, 1) (2, 1) (2, 0); // Warn human, but let them decide
    TERM HUMAN_ON_THE_LOOP := (2, 0) (2, 1) (3, 1) (3, 0); // Inform human about the problem
    TERM AI_AUTONOMOUSLY := (3,0) (3,1) (4,1) (4,0); // Let the robot continue trying
    METHOD : COG;        // Use 'Center Of Gravity' defuzzification method
    DEFAULT := 0;        // Default value is 0 (HUMAN_MANUALLY if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
    AND : MIN;        // Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)

    RULE 1 : IF robotBatteryStatus IS LOW THEN suggestedWorkSharingApproach IS HUMAN_MANUALLY WITH 1.0;
    RULE 2 : IF timeRobotIsBlocked IS SHORT AND robotBatteryStatus IS NOT LOW THEN suggestedWorkSharingApproach IS AI_AUTONOMOUSLY WITH 0.8;
    RULE 3 : IF timeRobotIsBlocked IS MODERATE AND robotBatteryStatus IS NOT LOW THEN suggestedWorkSharingApproach IS HUMAN_ON_THE_LOOP WITH 0.8;
    RULE 4 : IF timeRobotIsBlocked IS MODERATE AND waitingTimeForHuman IS LOW THEN suggestedWorkSharingApproach IS HUMAN_MANUALLY WITH 1.0;
    RULE 5 : IF timeRobotIsBlocked IS MODERATE AND waitingTimeForHuman IS HIGH THEN suggestedWorkSharingApproach IS HUMAN_IN_THE_LOOP WITH 0.8;
    RULE 6 : IF timeRobotIsBlocked IS LONG THEN suggestedWorkSharingApproach IS HUMAN_MANUALLY WITH 1.0;
END_RULEBLOCK

END_FUNCTION_BLOCK
