FUNCTION_BLOCK agricultureSchedulingSlidingDecisionRules

VAR_INPUT				// Define input variables
	distanceToCentralCollectionPoint : REAL;
	waitingTimeForDrone : REAL;
	fatigueLevelOfWorker : REAL;
END_VAR

VAR_OUTPUT				// Define output variable
	suggested_work_sharing_approach : REAL;
END_VAR

FUZZIFY distanceToCentralCollectionPoint
    TERM LOW :=  (0, 1) (5, 1) (15, 0);
    TERM HIGH := (5, 0) (15, 1) (20, 1);
END_FUZZIFY

FUZZIFY waitingTimeForDrone
    TERM LOW :=  (0, 1) (5, 1) (15, 0);
    TERM HIGH := (5, 0) (15, 1) (20, 1);
END_FUZZIFY

FUZZIFY fatigueLevelOfWorker
    TERM LOW :=  (0, 1) (5, 1) (15, 0);
    TERM HIGH := (5, 0) (15, 1) (20, 1);
END_FUZZIFY

DEFUZZIFY suggested_work_sharing_approach
	TERM HUMAN_MANUALLY := (0,1) (1,1) (1,0);           // let the worker carry the box
	TERM AI_AUTONOMOUSLY := (1,0) (1,1) (2,1) (2,0);    // let the drone carry the box
	TERM HUMAN_IN_THE_LOOP := (2,0) (2,1) (3,1) (3,0);  // let the worker decide
	TERM HUMAN_ON_THE_LOOP := (3,0) (3,1) (4,1) (4,0);  // inform supervisor, who may potentially intervene
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (HUMAN_MANUALLY if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)

	RULE 1 : IF distanceToCentralCollectionPoint IS LOW THEN suggested_work_sharing_approach IS HUMAN_MANUALLY;
    RULE 2 : IF distanceToCentralCollectionPoint IS HIGH OR waitingTimeForDrone IS LOW THEN suggested_work_sharing_approach IS AI_AUTONOMOUSLY;
    RULE 3 : IF waitingTimeForDrone IS HIGH AND fatigueLevelOfWorker IS LOW THEN suggested_work_sharing_approach IS HUMAN_MANUALLY;
    RULE 4 : IF waitingTimeForDrone IS HIGH AND distanceToCentralCollectionPoint IS HIGH AND fatigueLevelOfWorker IS LOW THEN suggested_work_sharing_approach IS HUMAN_IN_THE_LOOP;
    RULE 5 : IF waitingTimeForDrone IS HIGH AND fatigueLevelOfWorker IS HIGH THEN suggested_work_sharing_approach IS HUMAN_ON_THE_LOOP;

END_RULEBLOCK

END_FUNCTION_BLOCK
